buildscript {
    ext {
		springBootVersion = '1.4.2.RELEASE'
	}
	repositories {
		mavenCentral()
	}
	dependencies {
		classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
	}
}

apply plugin: 'maven'
apply plugin: 'eclipse'
apply plugin: 'java'
apply plugin: 'org.springframework.boot'


def description = 'JavaFx view loader library to easing the integration with SpringBoot'
def implementationTitle = "bootfx"
def author = 'Emerson Jose Morgado Brito'
def contactMail = "emerson.morgado@gmail.com"

def builNumber = System.getenv("BUILD_NUMBER") ?: '0-SNAPSHOT'
version = '1.0.'+builNumber


targetCompatibility = '1.8'
sourceCompatibility = '1.8'

[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

jar {
	baseName = 'bootfx'
}

repositories {
	jcenter() // necessary for fontAwesomeFX
    mavenCentral()
}


dependencies {

    compile 'org.slf4j:slf4j-api'
    compile 'org.slf4j:jcl-over-slf4j'
    
    // https://bintray.com/jerady/maven/FontAwesomeFX/
    // http://www.jensd.de/wordpress/?p=2476
	compile 'de.jensd:fontawesomefx-commons:8.12'
	compile 'de.jensd:fontawesomefx-controls:8.12'
	compile 'de.jensd:fontawesomefx-fontawesome:4.6.3'

	compile('org.springframework.boot:spring-boot-autoconfigure')
    
    testCompile 'junit:junit:4.12'
}

// http://docs.spring.io/autorepo/docs/spring-boot/current/reference/htmlsingle/#build-tool-plugins-gradle-repackage-configuration
bootRepackage {
	enabled = false
}

jar.doFirst {
    manifest {
        attributes "Specification-Title"  : name,
                "Specification-Version": version,
                "Specification-Vendor" : contactMail,
                "Implementation-Title" : name,
                "Implementation-Version" : version,
                "Implementation-Vendor": contactMail,
        		'Created-By': author,
        		'Description': description        		   
    }
}

uploadArchives {
    repositories {
    	mavenDeployer {
	    	repository( url: 'http://www.velumsoft.com.br/archiva/repository/internal/' ){
	    		authentication( userName: 'gradle', password: 'gr4dl3u53r1' )
	    	}

	    	pom.version = version
	    	pom.artifactId = 'bootfx'
	    	pom.groupId = 'br.com.emorgado'
	    	pom.name = implementationTitle
	    	pom.withXml {
		    	asNode().appendNode( 'description', description )
		    }
    	}
    }
}